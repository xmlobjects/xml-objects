import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'org.xmlobjects'
version = '1.2.0'
description = 'A simple and lightweight XML-to-object mapping library'
ext.date = new Date()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

repositories {
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}

dependencies {
    api 'org.glassfish.jaxb:xsom:4.0.6'
    api 'org.xmlobjects:classindex:3.13.1'
    annotationProcessor 'org.xmlobjects:classindex:3.13.1'
}

javadoc {
    options {
        overview = "$projectDir/build/tmp/javadoc/overview.html"
        header = "$project.name $project.version"
        bottom """
            <a target="_blank" href="https://github.com/xmlobjects/xml-objects/issues">Report a bug or suggest an enhancement</a><br />
            ${project.name} is open source and licensed under the <a target="_blank" href="https://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br />
            Official website and more information at: <a target="_blank" href="https://github.com/xmlobjects">https://github.com/xmlobjects</a><br />
            Copyright &copy; 2013-${date.format('yyyy')}<br />
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
        addStringOption('Xdoclint:none', '-quiet')
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$projectDir/build/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    name: name,
                    version: version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    from 'README.md'
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/xmlobjects/xml-objects'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/xmlobjects/xml-objects'
                    connection = 'scm:git:https://github.com/xmlobjects/xml-objects.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}