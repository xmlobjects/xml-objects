import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group 'org.xmlobjects'
version '1.0.0-SNAPSHOT'
description 'A simple and lightweight XML-to-object mapping library'
ext.date = new Date()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.glassfish.jaxb:xsom:3.0.2'
    api 'org.xmlobjects:classindex:3.11.1'
    annotationProcessor 'org.xmlobjects:classindex:3.11.1'
}

javadoc {
    options {
        overview "$buildDir/tmp/javadoc/overview.html"
        header "$project.name $project.version"
        bottom """
            <a target="_blank" href="https://github.com/xmlobjects/xml-objects/issues">Report a bug or suggest an enhancement</a><br />
            $project.name is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br />
            Official website and more information at: <a target="_blank" href="https://github.com/xmlobjects">https://github.com/xmlobjects</a><br />
            Copyright &copy; 2013-${date.format('yyyy')}<br />
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    name: project.name,
                    version: project.version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/xmlobjects/xml-objects'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/xmlobjects/xml-objects'
                    connection = 'scm:git:https://github.com/xmlobjects/xml-objects.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype()
    }
}